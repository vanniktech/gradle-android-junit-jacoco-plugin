buildscript {
  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.gradle.publish:plugin-publish-plugin:0.10.1'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.38.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.16.0'
  }
}

apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply plugin: "com.vanniktech.maven.publish"
apply plugin: 'com.gradle.plugin-publish'

gradlePlugin {
  plugins {
    androidJUnitJacocoPlugin {
      id = 'com.vanniktech.android.junit.jacoco'
      implementationClass = 'com.vanniktech.android.junit.jacoco.GenerationPlugin'
    }
  }
}

repositories {
  mavenCentral()
  google()
  jcenter()
}

dependencies {
  api gradleApi()
  api localGroovy()

  compileOnly 'com.android.tools.build:gradle:4.2.1'

  testCompile 'com.android.tools.build:gradle:4.2.1'
  testCompile 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.20'
  testCompile 'junit:junit:4.13'
  testCompile 'org.spockframework:spock-core:1.1-groovy-2.4', { exclude module: "groovy-all" } // Use localGroovy()
}

sourceCompatibility = JavaVersion.VERSION_1_8

pluginBundle {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    androidJUnitJacocoPlugin {
      displayName = POM_NAME
      tags = ['gradle', 'android', 'jacoco', 'app module', 'library module', 'junit', 'unit', 'testing', 'coverage']
      description = POM_DESCRIPTION
    }
  }
}

wrapper {
  gradleVersion = '6.8.3'
  distributionType = Wrapper.DistributionType.ALL
}
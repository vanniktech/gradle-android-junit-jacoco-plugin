buildscript {
    repositories {
        jcenter()
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.8.0-SNAPSHOT'
    }
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.vanniktech.android.junit.jacoco'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:2.3.3'

    testCompile 'junit:junit:4.12'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4', { exclude module: "groovy-all" } // Use localGroovy()
}

sourceCompatibility = JavaVersion.VERSION_1_7

group = GROUP
version = VERSION_NAME

pluginBundle {
    website = POM_URL
    vcsUrl = POM_SCM_URL

    plugins {
        androidJacoco {
            id = 'com.vanniktech.android.junit.jacoco'
            displayName = POM_NAME
            tags = ['gradle', 'android', 'jacoco', 'app module', 'library module', 'junit', 'unit', 'testing', 'coverage']
            description = POM_DESCRIPTION
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
    distributionType = Wrapper.DistributionType.ALL
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from 'build/docs/groovydoc'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives groovydocJar
    archives javadocJar
    archives sourcesJar
}

apply from: file('gradle/gradle-mvn-push.gradle')

apply plugin: 'maven-publish'

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}